apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    def STRING = 'String'

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.stefano.bookme"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    signingConfigs {
        release {
            Properties props = new Properties()
            if (file('../local.properties').exists()) {
                // if it doesn't exist, the props will all be null and the production builds will fail
                props.load(new FileInputStream(file('../local.properties')))
            }

            storeFile file(defaultConfig.applicationId + '.jks')
            storePassword props['keystore.storePass']
            keyAlias props['keystore.keyAlias']
            keyPassword props['keystore.keyPass']
        }

        debug {
            keyAlias 'development'
            storeFile file('development.jks')
            storePassword 'servis'
            keyPassword 'servis'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            debuggable true
        }
    }

    flavorDimensions 'api'

    productFlavors {
        staging {
            dimension 'api'
            applicationIdSuffix '.staging'
            signingConfig signingConfigs.debug
            buildConfigField STRING, "BASE_API_URL", "\"https://www.googleapis.com/books/v1/\""

        }

        production {
            dimension 'api'
            signingConfig signingConfigs.release
            buildConfigField STRING, "BASE_API_URL", "\"https://www.googleapis.com/books/v1/\""
        }

    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

ext {
    supportVersion = "28.0.0"
    constraintLayoutVersion = "1.1.3"
    multiDexVersion = "1.0.3"
    retrofitVersion = "2.4.0"
    okhttpLoggingInterceptor = "3.8.1"
    glideVersion = "3.8.0"
    daggerVersion = "2.16"
    threeTenVersion = "1.1.1"
    easyPermissionsVersion = "1.0.1"
    moshiVersion = "1.7.0"
    rxKotlinVersion = "2.2.0"
    rxAndroidVersion = "2.0.1"
    rxJavaVersion = "2.2.3"
    rxRelayVersion = "2.1.0"
    timberVersion = "4.7.1"
    paginateVersion = "0.5.1"
}

dependencies {

    //Libs
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //Rx
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rxRelayVersion"

    //Support
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:multidex:$multiDexVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    //Logging
    implementation "com.jakewharton.timber:timber:$timberVersion"

    //Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLoggingInterceptor"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

    //Dagger
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    //Parsing
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"

    //ImageUtils
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    //DateUtils
    implementation "com.jakewharton.threetenabp:threetenabp:$threeTenVersion"
    //here because Unit tests break when you don't have this in the project - https://github.com/JakeWharton/ThreeTenABP/issues/14
    testImplementation("org.threeten:threetenbp:$threeTenVersion") {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }

    //UI
    implementation 'com.eftimoff:android-pathview:1.0.8@aar'

    //Pagination
    implementation "com.github.markomilos:paginate:$paginateVersion"

    //Permissions
    implementation "pub.devrel:easypermissions:$easyPermissionsVersion"

    //Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
